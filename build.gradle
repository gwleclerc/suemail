group 'org.gwleclerc'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.1'
    ext.jira_client_version = '4.0.0'
    ext.javax_mail_version = '1.5.6'
    ext.jsoup_version = '1.10.2'
    ext.fugue_version = '1.1'
    ext.fuel_version = '1.5.0'
    ext.result_version = '1.0.5'
    ext.klaxon_version = '0.27'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://maven.atlassian.com/content/repositories/atlassian-public"
    }
}

configurations {
    ktlint
}

// Building a Jar with dependencies with Kotlin and Gradle: http://www.preslav.me/2016/06/13/kotlin-basics-create-an-executable-kotlin-jar-using-gradle/
jar {
    manifest {
        attributes 'Main-Class': 'org.gwleclerc.suemail.MainKt'
    }
    // This line of code recursively collects and copies all of a project's files
    // and adds them to the JAR itself. One can extend this task, to skip certain
    // files or particular types at will
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.atlassian.jira:jira-rest-java-client-core:$jira_client_version"
    compile "com.sun.mail:javax.mail:$javax_mail_version"
    compile "org.jsoup:jsoup:$jsoup_version"
    compile "com.atlassian.fugue:fugue:$fugue_version"
    compile "com.github.kittinunf.fuel:fuel:$fuel_version"
    compile "com.github.kittinunf.fuel:fuel-android:$fuel_version"
    compile "com.github.kittinunf.result:result:$result_version"
    compile "com.beust:klaxon:$klaxon_version"
    ktlint 'com.github.shyiko:ktlint:0.6.1'

}

// TODO Consider switching to https://github.com/JLLeitschuh/ktlint-gradle Gradle plugin instead
task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
